# Maintainer: katt <magunasu.b97@gmail.com>

pkgname=duckstation-git
pkgver=r3588.43bb69fb
pkgdesc='A Sony PlayStation (PSX) emulator, focusing on playability, speed, and long-term maintainability'
pkgrel=1
arch=(x86_64 aarch64)
url=https://github.com/stenzek/duckstation
license=(GPLv3)
makedepends=(cmake git)
depends=(gtk3 sdl2 qt5-base qt5-tools)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=(git+"${url}".git)
md5sums=('SKIP')

pkgver() {
    cd "${pkgname%-git}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cmake -B build -S "${pkgname%-git}" \
        -DCMAKE_BUILD_TYPE=None \
        -Wno-dev
    make -C build
}

package() {
    install -Dm755 -t "${pkgdir}/usr/bin" build/bin/"${pkgname%-git}"-{sdl,qt}

    install -Dm644 -t "${pkgdir}/usr/share/applications" "${pkgname%-git}"/appimage/"${pkgname%-git}"-{sdl,qt}.desktop
    sed -e 's/Terminal=true/Terminal=false/' -e 's/Icon=duckstation-qt/Icon=duckstation/' -i "${pkgdir}/usr/share/applications/${pkgname%-git}-qt.desktop"
    sed -e 's/Terminal=true/Terminal=false/' -e 's/Icon=duckstation-sdl/Icon=duckstation/' -i "${pkgdir}/usr/share/applications/${pkgname%-git}-sdl.desktop"

    install -Dm644 "${pkgname%-git}"/appimage/icon-64px.png "${pkgdir}/usr/share/pixmaps/duckstation.png"
}
