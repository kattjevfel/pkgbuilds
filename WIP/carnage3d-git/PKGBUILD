# Maintainer: katt <magunasu.b97@gmail.com>

pkgname=carnage3d-git
pkgver=r607.3590a4e
pkgrel=1
pkgdesc='Reimplementation of Grand Theft Auto 1'
arch=(x86_64)
url=https://github.com/codenamecpp/carnage3d
license=(MIT)
depends=(glew box2d glfw openal)
makedepends=(git cmake)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=(git+"${url}".git)
md5sums=('SKIP')

pkgver() {
	cd "${pkgname%-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cmake -B build -S "${pkgname%-git}" \
		-DCMAKE_INSTALL_PREFIX='/usr' \
		-DWITH_BOX2D=OFF \
		-Wno-dev
	cmake --build build
}

package() {
	#make DESTDIR="$pkgdir/" -C "${pkgname%-git}" install
	DESTDIR="${pkgdir}" cmake --install build
}
