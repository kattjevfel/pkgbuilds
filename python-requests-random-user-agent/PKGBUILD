# Maintainer: katt <magunasu.b97@gmail.com>

pkgname=python-requests-random-user-agent
pkgver=2022.05.03
pkgrel=1
pkgdesc='Configures the requests library to randomly select a desktop User-Agent'
arch=(any)
url=https://github.com/DavidWittman/requests-random-user-agent
license=(MIT)
depends=(python python-requests)
makedepends=(python-build python-installer python-setuptools python-wheel)
optdepends=('ffmpeg: Convert Pixiv Ugoira to WebM'
            'youtube-dl: Download videos'
            'python-pysocks: SOCKS support')
source=(${pkgname}-${pkgver}.tar.gz::"${url}"/archive/refs/tags/${pkgver}.tar.gz)
sha512sums=('a08d71fbd7664cc277b2b0447c0c137b63840ba3a253f703043832cce81c13e2b2e0da86639126dbd91b27b4f41c8ebeb03a9382c6b72a1a50afdedf987a7b14')

build() {
    cd ${pkgname#python-}-${pkgver}
    python -m build --wheel --no-isolation
}

check() {
    cd ${pkgname#python-}-${pkgver}
    python -m unittest discover tests/
}

package() {
    cd ${pkgname#python-}-${pkgver}
    python -m installer --destdir="$pkgdir" dist/*.whl
    install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
