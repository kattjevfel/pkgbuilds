# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=lib32-pipewire-katt-git
pkgname=(lib32-pipewire-katt-git lib32-pipewire-jack-katt-git lib32-pipewire-v4l2-katt-git)
pkgver=0.3.41.r4.g57dae3e2b
pkgrel=1
pkgdesc='Low-latency audio/video router and processor - 32-bit client library (git)'
url=https://pipewire.org
license=(MIT)
arch=(x86_64)
makedepends=(git meson lib32-alsa-lib lib32-systemd lib32-dbus)
source=(git+https://gitlab.freedesktop.org/pipewire/pipewire.git)
sha256sums=('SKIP')

pkgver() {
    git -C pipewire describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    local meson_options=(
        --libdir /usr/lib32
        -D examples=disabled
        -D man=disabled
        -D tests=disabled
        -D gstreamer=disabled
        -D gstreamer-device-provider=disabled
        -D systemd-user-service=disabled
        -D bluez5=disabled
        -D jack=disabled
        -D v4l2=disabled
        -D libcamera=disabled
        -D pw-cat=disabled
        -D udevrulesdir=/usr/lib/udev/rules.d
        -D sdl2=disabled
        -D sndfile=disabled
        -D libpulse=disabled
        -D roc=disabled
        -D avahi=disabled
        -D echo-cancel-webrtc=disabled
        -D libusb=disabled
        -D session-managers=[]
        -D raop=disabled
        -D lv2=disabled
    )

    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

    arch-meson pipewire build "${meson_options[@]}"
    meson compile -C build
}

check() {
    meson test -C build --print-errorlogs
}

_pick() {
    local p="$1" f d;
    shift
    for f; do
        d="$srcdir/$p/${f#$pkgdir/}"
        mkdir -p "$(dirname "$d")"
        mv "$f" "$d"
        rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
    done
}

_ver=${pkgver:0:3}

package_lib32-pipewire-katt-git() {
    depends=(pipewire-katt-git lib32-systemd libdbus-1.so libasound.so)
    optdepends=('lib32-pipewire-jack-katt-git: JACK support'
                'lib32-pipewire-v4l2-katt-git: V4L2 interceptor')
    provides=("${pkgname%-katt-git}" libpipewire-$_ver.so)
    conflicts=("${pkgname%-katt-git}")

    meson install -C build --destdir "$pkgdir"

    (
        cd "$pkgdir"

        _pick jack usr/lib32/pipewire-$_ver/jack

        _pick v4l2 usr/lib32/pipewire-$_ver/v4l2

        rm -r usr/{bin,include,lib,share}
    )

    install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

package_lib32-pipewire-jack-katt-git() {
    pkgdesc+=" - JACK support"
    license+=(GPL2) # libjackserver
    depends=(pipewire-jack-katt-git lib32-pipewire-katt-git libpipewire-$_ver.so)
    provides=("${pkgname%-katt-git}")
    conflicts=("${pkgname%-katt-git}")

    mv jack/* "$pkgdir"

    install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

package_lib32-pipewire-v4l2-katt-git() {
    pkgdesc+=" - V4L2 interceptor"
    depends=(pipewire-v4l2-katt-git lib32-pipewire-katt-git libpipewire-$_ver.so)
    provides=("${pkgname%-katt-git}")
    conflicts=("${pkgname%-katt-git}")

    mv v4l2/* "$pkgdir"

    install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}
