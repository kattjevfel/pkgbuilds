# Maintainer: katt <magunasu.b97@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=pipewire-katt-git
pkgname=(pipewire-katt-git alsa-card-profiles-katt-git pipewire-docs-katt-git pipewire-alsa-katt-git pipewire-jack-katt-git pipewire-pulse-katt-git gst-plugin-pipewire-katt-git pipewire-zeroconf-katt-git pipewire-v4l2-katt-git pipewire-x11-bell-katt-git)
pkgver=0.3.54.r16.ga1fac201e
pkgrel=1
epoch=1
pkgdesc='Low-latency audio/video router and processor (git)'
url=https://pipewire.org
license=(MIT)
arch=(x86_64)
makedepends=(alsa-lib bluez-libs avahi git dbus doxygen graphviz gst-plugins-base libcanberra libfdk-aac libfreeaptx libldac libpulse libsndfile libusb libx11 libxfixes lilv meson ncurses python-docutils rtkit sbc sdl2 valgrind webrtc-audio-processing)
checkdepends=(desktop-file-utils)
source=(git+https://gitlab.freedesktop.org/pipewire/pipewire.git)
md5sums=('SKIP')

pkgver() {
    git -C ${pkgbase%-katt-git} describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    # remove export of LD_LIBRARY_PATH for pw-jack as it would add /usr/lib
    sed -i '/LD_LIBRARY_PATH/d' ${pkgbase%-katt-git}/pipewire-jack/src/pw-jack.in
}

build() {
    local meson_options=(
        -D bluez5-codec-lc3plus=disabled
        -D docs=enabled
        -D jack=disabled
        -D jack-devel=true
        -D libjack-path=/usr/lib
        -D libcamera=disabled
        -D udevrulesdir=/usr/lib/udev/rules.d
        -D roc=disabled
        -D session-managers=[]
    )
    
    arch-meson pipewire build "${meson_options[@]}"
    meson compile -C build
}

check() {
    meson test -C build --print-errorlogs
}

_pick() {
    local p="$1" f d; shift
    for f; do
        d="$srcdir/$p/${f#$pkgdir/}"
        mkdir -p "$(dirname "$d")"
        mv "$f" "$d"
        rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
    done
}

_ver=${pkgver:0:3}

package_pipewire-katt-git() {
    license+=(LGPL) # libspa-alsa
    depends=(alsa-card-profiles-katt-git libdbus-1.so libncursesw.so libsndfile.so libudev.so libusb-1.0.so libasound.so libsystemd.so libbluetooth.so libsbc.so libldacBT_enc.so libfreeaptx.so libfdk-aac.so libwebrtc_audio_processing.so liblilv-0.so)
    optdepends=('pipewire-docs-katt-git: Documentation'
                'pipewire-session-manager: Session manager'
                'pipewire-alsa-katt-git: ALSA configuration'
                'pipewire-jack-katt-git: JACK support'
                'pipewire-pulse-katt-git: PulseAudio replacement'
                'gst-plugin-pipewire-katt-git: GStreamer plugin'
                'pipewire-zeroconf-katt-git: Zeroconf support'
                'pipewire-v4l2-katt-git: V4L2 interceptor'
                'pipewire-x11-bell-katt-git: X11 bell'
                'realtime-privileges: realtime privileges with rt module'
                'rtkit: realtime privileges with rtkit module')
    provides=("${pkgname%-katt-git}=${pkgver}" libpipewire-$_ver.so)
    conflicts=("${pkgname%-katt-git}")
    install=pipewire.install

    meson install -C build --destdir "$pkgdir"

    (
        cd "$pkgdir"

        _pick acp usr/lib/udev
        _pick acp usr/share/alsa-card-profile

        _pick docs usr/share/doc

        _pick jack usr/bin/pw-jack
        _pick jack usr/include/jack
        _pick jack usr/lib/libjack*
        _pick jack usr/lib/pkgconfig/jack.pc
        _pick jack usr/share/man/man1/pw-jack.1
        _pick jack usr/share/pipewire/jack.conf

        _pick pulse usr/bin/pipewire-pulse
        _pick pulse usr/lib/pipewire-$_ver/libpipewire-module-protocol-pulse.so
        _pick pulse usr/lib/pipewire-$_ver/libpipewire-module-pulse-tunnel.so
        _pick pulse usr/lib/systemd/user/pipewire-pulse.*

        _pick gst usr/lib/gstreamer-1.0

        _pick zeroconf usr/lib/pipewire-$_ver/libpipewire-module-{raop,zeroconf}-discover.so

        _pick v4l2 usr/bin/pw-v4l2 usr/lib/pipewire-$_ver/v4l2

        _pick x11-bell usr/lib/pipewire-$_ver/libpipewire-module-x11-bell.so

        mkdir -p etc/alsa/conf.d
        ln -st etc/alsa/conf.d /usr/share/alsa/alsa.conf.d/50-pipewire.conf

        # directories for overrides
        mkdir -p etc/pipewire/{client-rt,client,minimal,pipewire,pipewire-pulse}.conf.d
    )

    install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 ${pkgbase%-katt-git}/COPYING
}

package_alsa-card-profiles-katt-git() {
    pkgdesc+=' - ALSA card profiles (git)'

    license=(LGPL)
    provides=("${pkgname%-katt-git}")
    conflicts=("${pkgname%-katt-git}")

    mv acp/* "$pkgdir"
}

package_pipewire-docs-katt-git() {
    pkgdesc+=' - documentation (git)'
    provides=("${pkgname%-katt-git}")
    conflicts=("${pkgname%-katt-git}")

    mv docs/* "$pkgdir"

    install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 ${pkgbase%-katt-git}/COPYING
}

package_pipewire-alsa-katt-git() {
    pkgdesc+=' - ALSA configuration (git)'
    depends=(pipewire-session-manager)
    provides=("${pkgname%-katt-git}" pulseaudio-alsa)
    conflicts=("${pkgname%-katt-git}")

    mkdir -p "$pkgdir/etc/alsa/conf.d"
    ln -st "$pkgdir/etc/alsa/conf.d" \
        /usr/share/alsa/alsa.conf.d/99-pipewire-default.conf

    install -Dm644 /dev/null "$pkgdir/usr/share/pipewire/media-session.d/with-alsa"

    install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 ${pkgbase%-katt-git}/COPYING
}

package_pipewire-jack-katt-git() {
    pkgdesc+=' - JACK support (git)'
    license+=(GPL2) # libjackserver
    depends=(pipewire-session-manager libpipewire-$_ver.so sh)
    optdepends=('jack-example-tools: for official JACK example-clients and tools')
    provides=("${pkgname%-katt-git}" jack libjack.so libjackserver.so libjacknet.so)
    conflicts=("${pkgname%-katt-git}" jack jack2)

    mv jack/* "$pkgdir"

    install -Dm644 /dev/null "$pkgdir/usr/share/pipewire/media-session.d/with-jack"

    # directories for overrides
    mkdir -p "$pkgdir/etc/pipewire/jack.conf.d"

    install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 ${pkgbase%-katt-git}/COPYING
}

package_pipewire-pulse-katt-git() {
    pkgdesc+=' - PulseAudio replacement (git)'
    depends=(pipewire-session-manager libpipewire-$_ver.so libpulse.so libavahi-{client,common}.so)
    provides=("${pkgname%-katt-git}" pulseaudio pulseaudio-bluetooth)
    conflicts=("${pkgname%-katt-git}" pulseaudio pulseaudio-bluetooth)
    install=pipewire-pulse.install

    mv pulse/* "$pkgdir"

    install -Dm644 /dev/null "$pkgdir/usr/share/pipewire/media-session.d/with-pulseaudio"

    install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 ${pkgbase%-katt-git}/COPYING
}

package_gst-plugin-pipewire-katt-git() {
    pkgdesc='Multimedia graph framework - pipewire plugin (git)'
    depends=(pipewire-session-manager libpipewire-$_ver.so gst-plugins-base-libs)
    provides=("${pkgname%-katt-git}")
    conflicts=("${pkgname%-katt-git}")

    mv gst/* "$pkgdir"

    install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 ${pkgbase%-katt-git}/COPYING
}

package_pipewire-zeroconf-katt-git() {
    pkgdesc+=' - Zeroconf support (git)'
    depends=(pipewire libpipewire-$_ver.so libavahi-{client,common}.so)
    provides=("${pkgname%-katt-git}")
    conflicts=("${pkgname%-katt-git}")

    mv zeroconf/* "$pkgdir"

    install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 ${pkgbase%-katt-git}/COPYING
}

package_pipewire-v4l2-katt-git() {
    pkgdesc+=' - V4L2 interceptor (git)'
    depends=(pipewire-session-manager libpipewire-$_ver.so sh)
    provides=("${pkgname%-katt-git}")
    conflicts=("${pkgname%-katt-git}")

    mv v4l2/* "$pkgdir"

    install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 ${pkgbase%-katt-git}/COPYING
}

package_pipewire-x11-bell-katt-git() {
    pkgdesc+=' - X11 bell'
    depends=(pipewire libpipewire-$_ver.so libx11 libxfixes libcanberra.so)
    provides=("${pkgname%-katt-git}")
    conflicts=("${pkgname%-katt-git}")

    mv x11-bell/* "$pkgdir"

    install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 ${pkgbase%-katt-git}/COPYING
}
