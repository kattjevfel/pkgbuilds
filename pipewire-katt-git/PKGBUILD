# Maintainer: katt <magunasu.b97@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=pipewire-katt-git
pkgname=(pipewire-katt-git alsa-card-profiles-katt-git pipewire-docs-katt-git pipewire-alsa-katt-git pipewire-media-session-katt-git
        pipewire-jack-katt-git pipewire-pulse-katt-git gst-plugin-pipewire-katt-git pipewire-zeroconf-katt-git)
pkgver=0.3.28.r0.gfb55cc47
pkgrel=1
epoch=1
pkgdesc='Low-latency audio/video router and processor (git)'
url=https://pipewire.org
license=(MIT)
arch=(x86_64)
makedepends=(git meson doxygen xmltoman valgrind libpulse alsa-lib
            gst-plugins-base rtkit dbus sdl2 ncurses libsndfile bluez-libs sbc
            libldac libopenaptx libfdk-aac vulkan-headers vulkan-icd-loader
            avahi)
checkdepends=(desktop-file-utils)
source=(git+https://gitlab.freedesktop.org/pipewire/pipewire.git)
md5sums=('SKIP')

pkgver() {
    git -C ${pkgbase%-katt-git} describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    arch-meson ${pkgbase%-katt-git} build \
        -D docs=enabled \
        -D jack=disabled \
        -D libcamera=disabled \
        -D udevrulesdir=/usr/lib/udev/rules.d
    meson compile -C build
}

check() {
    meson test -C build --print-errorlogs
}

_pick() {
    local p="$1" f d
    shift
    for f; do
        d="$srcdir/$p/${f#$pkgdir/}"
        mkdir -p "$(dirname "$d")"
        mv "$f" "$d"
        rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
    done
}

_ver=${pkgver:0:3}

package_pipewire-katt-git() {
    license+=(LGPL)  # libspa-alsa
    depends=(rtkit alsa-card-profiles-katt-git libdbus-1.so libncursesw.so libsndfile.so
            libudev.so libasound.so libsystemd.so libbluetooth.so libsbc.so
            libldacBT_enc.so libopenaptx.so libfdk-aac.so libvulkan.so)
    optdepends=('pipewire-docs-katt-git: Documentation'
                'pipewire-media-session-katt-git: Default session manager'
                'pipewire-alsa-katt-git: ALSA configuration'
                'pipewire-jack-katt-git: JACK support'
                'pipewire-pulse-katt-git: PulseAudio replacement'
                'gst-plugin-pipewire-katt-git: GStreamer support')
    provides=("${pkgname%-katt-git}" libpipewire-$_ver.so)
    conflicts=("${pkgname%-katt-git}")
    install=pipewire.install

    meson install -C build --destdir "$pkgdir"

    install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 ${pkgbase%-katt-git}/COPYING

    cd "$pkgdir"

    _pick acp usr/lib/udev
    _pick acp usr/share/alsa-card-profile

    _pick docs usr/share/doc

    _pick pms usr/bin/pipewire-media-session
    _pick pms usr/lib/systemd/user/pipewire-media-session.service
    _pick pms usr/share/pipewire/media-session.d/*.conf

    _pick jack usr/bin/pw-jack usr/lib/pipewire-$_ver/jack
    _pick jack usr/share/man/man1/pw-jack.1
    _pick jack usr/share/pipewire/{jack.conf,media-session.d/with-jack}

    _pick pulse usr/lib/pipewire-$_ver/libpipewire-module-pulse-tunnel.so
    _pick pulse usr/share/pipewire/media-session.d/with-pulseaudio

    _pick gst usr/lib/gstreamer-1.0

    _pick zeroconf usr/lib/pipewire-$_ver/libpipewire-module-zeroconf-discover.so
}

package_alsa-card-profiles-katt-git() {
    pkgdesc+=' - ALSA card profiles (git)'

    license=(LGPL)
    provides=("${pkgname%-katt-git}")
    conflicts=("${pkgname%-katt-git}")

    mv acp/* "$pkgdir"
}

package_pipewire-docs-katt-git() {
    pkgdesc+=' - documentation (git)'
    provides=("${pkgname%-katt-git}")
    conflicts=("${pkgname%-katt-git}")

    mv docs/* "$pkgdir"

    install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 ${pkgbase%-katt-git}/COPYING
}

package_pipewire-media-session-katt-git() {
    pkgdesc+=' - Session manager (git)'
    depends=(pipewire-katt-git libpipewire-$_ver.so)
    install=pipewire-media-session.install
    provides=("${pkgname%-katt-git}")
    conflicts=("${pkgname%-katt-git}")

    mv pms/* "$pkgdir"

    install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 ${pkgbase%-katt-git}/COPYING
}

package_pipewire-alsa-katt-git() {
    pkgdesc+=' - ALSA configuration (git)'
    depends=(pipewire-media-session-katt-git)
    provides=("${pkgname%-katt-git}" pulseaudio-alsa)
    conflicts=("${pkgname%-katt-git}")

    mkdir -p "$pkgdir/etc/alsa/conf.d"
    ln -st "$pkgdir/etc/alsa/conf.d" \
        /usr/share/alsa/alsa.conf.d/{50-pipewire,99-pipewire-default}.conf

    install -Dm644 /dev/null "$pkgdir/usr/share/pipewire/media-session.d/with-alsa"

    install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 ${pkgbase%-katt-git}/COPYING
}

package_pipewire-jack-katt-git() {
    pkgdesc+=' - JACK support (git)'
    license+=(GPL2)  # libjackserver
    depends=(pipewire-media-session-katt-git libpipewire-$_ver.so)
    provides=("${pkgname%-katt-git}")
    conflicts=("${pkgname%-katt-git}")

    mv jack/* "$pkgdir"

    install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 ${pkgbase%-katt-git}/COPYING
}

package_pipewire-pulse-katt-git() {
    pkgdesc+=' - PulseAudio replacement (git)'
    depends=(pipewire-media-session-katt-git libpulse)
    provides=("${pkgname%-katt-git}" pulseaudio pulseaudio-bluetooth)
    conflicts=("${pkgname%-katt-git}" pulseaudio pulseaudio-bluetooth)
    install=pipewire-pulse.install

    mv pulse/* "$pkgdir"

    install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 ${pkgbase%-katt-git}/COPYING
}

package_gst-plugin-pipewire-katt-git() {
    pkgdesc='Multimedia graph framework - pipewire plugin (git)'
    depends=(pipewire-media-session-katt-git libpipewire-$_ver.so gst-plugins-base-libs)
    provides=("${pkgname%-katt-git}")
    conflicts=("${pkgname%-katt-git}")

    mv gst/* "$pkgdir"

    install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 ${pkgbase%-katt-git}/COPYING
}

package_pipewire-zeroconf-katt-git() {
    pkgdesc+=" - Zeroconf support (git)"
    depends=(pipewire libpipewire-$_ver.so libavahi-{client,common}.so)
    provides=("${pkgname%-katt-git}")
    conflicts=("${pkgname%-katt-git}")

    mv zeroconf/* "$pkgdir"

    install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 ${pkgbase%-katt-git}/COPYING
}
