# Maintainer: katt <magunasu.b97@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio-noslowremote
pkgver=5.91.0
pkgrel=1
pkgdesc='Resource and network access abstraction (reverting patch labeling slow files as remote files)'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(solid kjobwidgets kbookmarks libxslt kwallet ktextwidgets kded)
makedepends=(extra-cmake-modules kdoctools doxygen qt5-tools qt5-doc git)
optdepends=('kio-extras: extra protocols support (sftp, fish and more)' 'kdoctools: for the help kioslave'
            'kio-fuse: to mount remote filesystems via FUSE')
provides=("${pkgname%-noslowremote}")
conflicts=("${pkgname%-noslowremote}")
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname%-noslowremote}-$pkgver.tar.xz{,.sig}
        0001-Revert-Consider-slow-files-as-remote-files-in-previe.patch)
sha256sums=('f2ddea299f3dc98835df445dd1d622d90233115ad0200e75da5e954523466293'
            'SKIP'
            '94902cf263dba07460a54059fb44081b799756685ed30a5527970beaed0defcf')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    patch --directory="${pkgname%-noslowremote}"-$pkgver --forward --strip=1 --input="${srcdir}/0001-Revert-Consider-slow-files-as-remote-files-in-previe.patch"
}

build() {
    cmake -B build -S "${pkgname%-noslowremote}"-$pkgver \
        -DCMAKE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF \
        -DBUILD_QCH=ON
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
}
