# Maintainer: katt <magunasu.b97@gmail.com>

pkgname=weechat-relay-git
pkgver=r59.f827817
pkgrel=1
pkgdesc='WeeChat Relay protocol library, written in C'
arch=(x86_64)
url=https://weechat.org
license=(GPL)
depends=(gnutls)
makedepends=(cmake git)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=(git+https://github.com/weechat/"${pkgname%-git}")
md5sums=('SKIP')

pkgver() {
	cd "${pkgname%-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cmake -B build -S "${pkgname%-git}" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DBUILD_TESTS=ON
    cmake --build build
}

check() {
	cd build
	ctest -V
}

package() {
    DESTDIR="${pkgdir}" cmake --install build
}
