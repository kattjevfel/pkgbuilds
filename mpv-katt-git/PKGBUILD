# Maintainer: katt <magunasu.b97@gmail.com>

pkgname=mpv-katt-git
pkgver=0.38.0.r532.gcd1b63f628
pkgrel=1
pkgdesc='a free, open source, and cross-platform media player (git, biased af)'
arch=(x86_64)
url=https://mpv.io
license=(GPL-2.0-or-later AND LGPL-2.1-or-later)
depends=(desktop-file-utils ffmpeg libavcodec.so libavdevice.so libavfilter.so libavformat.so libavutil.so libswresample.so libswscale.so glibc hicolor-icon-theme lcms2 liblcms2.so libarchive libarchive.so libass libass.so libdrm libegl libgl libglvnd libjpeg libjpeg.so libplacebo libplacebo.so libva libva.so libva-drm.so libva-wayland.so libva-x11.so libvdpau libx11 libxext libxkbcommon libxkbcommon.so libxpresent libxrandr libxss libxv luajit mesa libpipewire rubberband librubberband.so uchardet vapoursynth vulkan-icd-loader wayland zlib)
makedepends=(git meson python-docutils ladspa wayland-protocols ffnvcodec-headers vulkan-headers)
optdepends=('yt-dlp: for video-sharing websites playback')
provides=("${pkgname%-katt-git}" libmpv.so)
conflicts=("${pkgname%-katt-git}")
options=('!emptydirs')
source=(git+https://github.com/mpv-player/mpv.git)
sha256sums=('SKIP')

pkgver() {
    git -C "${pkgname%-katt-git}" describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    local _meson_options=(
        --auto-features auto

        -Dlibmpv=true

        -Dcaca=disabled
        -Dlibarchive=enabled
    )
    arch-meson "${pkgname%-katt-git}" build "${_meson_options[@]}"
    meson compile -C build
}

check() {
    meson test -C build
}


package() {
    meson install -C build --destdir "${pkgdir}"

    # delete private entries only required for static linking
    sed -i -e '/Requires.private/d' -e '/Libs.private/d' "${pkgdir}"/usr/lib/pkgconfig/mpv.pc

    install -m0644 "${pkgname%-katt-git}"/DOCS/{encoding.rst,tech-overview.txt} \
        "${pkgdir}"/usr/share/doc/mpv

    install -m0644 "${pkgname%-katt-git}"/TOOLS/{umpv,mpv_identify.sh,stats-conv.py,idet.sh,lua/*} \
        -D -t "${pkgdir}"/usr/share/mpv/scripts
}
