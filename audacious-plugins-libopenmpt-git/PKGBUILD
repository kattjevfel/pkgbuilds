# Maintainer: katt <magunasu.b97@gmail.com>
# Contributor: Radioactiveman <thomas-lange2@gmx.de>
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=audacious-plugins-libopenmpt-git
pkgver=4.0.beta1.r201.gdf3a07781
pkgrel=1
pkgdesc='Plugins for Audacious (with libopenpt) (git version)'
arch=(i686 x86_64)
url='https://audacious-media-player.org'
license=(BSD GPL)
provides=("${pkgname%-*-*}")
conflicts=("${pkgname%-*-*}")
depends=(audacious-git libopenmpt)
makedepends=(glib2 python alsa-lib pulseaudio jack lame libvorbis flac mpg123 faad2 ffmpeg libmodplug fluidsynth libcdio-paranoia libsidplayfp wavpack libsamplerate libnotify lirc curl libmtp neon libmms libcue libbs2b git)
optdepends=(
    'alsa-lib: Advanced Linux Sound Arch. output'
    'pulseaudio: PulseAudio output'
    'jack: Jack Audio Connection Kit output'
    'lame: FileWriter MP3 output'
    'libvorbis: Vorbis input, FileWriter Vorbis output'
    'flac: FLAC input, FileWriter FLAC output'
    \
    'mpg123: MP3 input'
    'faad2: AAC input'
    'ffmpeg: ffaudio input'
    'libmodplug: modplug input'
    'fluidsynth: MIDI FluidSynth backend input'
    'libcdio-paranoia: CD Digital Audio input'
    'libsidplayfp: Commodore 64 audio input'
    'wavpack: WavPack input'
    \
    'libsamplerate: Speed and Pitch Plugin'
    'libnotify: libnotify OSD'
    'lirc: LIRC'
    'curl: AudioScrobbler Client'
    'libmtp: Upload to MTP device'
    'libbs2b: Bauer stereophonic-to-binaural DSP'
    \
    'neon: neon-based http transport'
    'libmms: libmms-based mms transport'
    'libcue: CUE playlist format'
)
source=(git+https://github.com/audacious-media-player/"${pkgname%-*-*}".git)
sha256sums=(SKIP)

pkgver() {
    cd "${pkgname%-*-*}"
    git describe --long --tags | sed 's/^audacious-plugins-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "${pkgname%-*-*}"
    autoreconf -I m4
    ./configure --prefix=/usr #--enable-gtk
    make
}

package() {
    cd "${pkgname%-*-*}"
    make DESTDIR="${pkgdir}" install
    install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
