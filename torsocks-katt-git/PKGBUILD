# Maintainer: katt <magunasu.b97@gmail.com>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Nicolas Pouillard <nicolas.pouillard@gmail.com>
# Contributor: Rorschach <r0rschach@lavabit.com>

pkgname=torsocks-katt-git
pkgver=2.3.0.r6.g67cee6c
pkgrel=1
pkgdesc='Wrapper to safely torify applications (git)'
url=https://gitweb.torproject.org/torsocks.git
arch=(x86_64)
license=(GPL2)
depends=(tor)
makedepends=(git)
provides=("${pkgname%-katt-git}")
conflicts=("${pkgname%-katt-git}")
backup=("etc/tor/${pkgname}.conf")
source=(git+https://git.torproject.org/torsocks.git)
sha512sums=('SKIP')

pkgver() {
    git -C "${pkgname%-katt-git}" describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "${pkgname%-katt-git}"
    ./autogen.sh
}

build() {
    cd "${pkgname%-katt-git}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc
    make
}

package() {
    cd "${pkgname%-katt-git}"
    make DESTDIR="${pkgdir}" install
}
